## ALT: Model 16: Full ALT-SR with covariates (fixed cls)

# define intercept and slope factor
i_cin =~ 1*cin_2011 + 1*cin_2012 + 1*cin_2013 + 1*cin_2014 + 1*cin_2015 + 1*cin_2016 + 1*cin_2017 + 1*cin_2018 + 1*cin_2019
i1_exp =~ 1*ehfs_sp_2011 + 1*ehfs_sp_2012 + 1*ehfs_sp_2013 + 1*ehfs_sp_2014 + 1*ehfs_sp_2015 + 1*ehfs_sp_2016 + 1*ehfs_sp_2017 + 1*ehfs_sp_2018 + 1*ehfs_sp_2019

s_cin =~ 0*cin_2011 + 1*cin_2012 + 2*cin_2013 + 3*cin_2014 + 4*cin_2015 + 5*cin_2016 + 6*cin_2017 + 7*cin_2018 + 8*cin_2019
s1_exp =~ 0*ehfs_sp_2011 + 1*ehfs_sp_2012 + 2*ehfs_sp_2013 + 3*ehfs_sp_2014 + 4*ehfs_sp_2015 + 5*ehfs_sp_2016 + 6*ehfs_sp_2017 + 7*ehfs_sp_2018 + 8*ehfs_sp_2019

q_exp =~ 0*ehfs_sp_2011 + 1*ehfs_sp_2012 + 4*ehfs_sp_2013 + 9*ehfs_sp_2014 + 16*ehfs_sp_2015 + 25*ehfs_sp_2016 + 36*ehfs_sp_2017 + 49*ehfs_sp_2018 + 64*ehfs_sp_2019

i_cin ~ imd 
s_cin ~ imd 

i1_exp ~ imd 
s1_exp ~ imd 

# estimate intercept and slope factor variance 
i_cin ~~ var_i_cin*i_cin
s_cin ~~ var_s_cin*s_cin
i_cin ~~ 0*q_exp
s_cin ~~ 0*q_exp
i1_exp ~~ var_i_exp*i1_exp
s1_exp ~~ var_s_exp*s1_exp
q_exp ~~ 0*q_exp

# estimate intercept and slope factor covariance
i_cin ~~ cov_i_s_cin*s_cin
i1_exp ~~ cov_i1_s1_exp*s1_exp
i1_exp ~~ 0*q_exp
s1_exp ~~ 0*q_exp

# between factors
i_cin ~~ cov_icin_iexp*i1_exp
i_cin ~~ cov_icin_s1exp*s1_exp
s_cin ~~ cov_scin_iexp*i1_exp
s_cin ~~ cov_scin_s1exp*s1_exp

# Autoregressive effects
cin_2012 ~ a2*cin_2011
cin_2013 ~ a2*cin_2012
cin_2014 ~ a2*cin_2013
cin_2015 ~ a2*cin_2014
cin_2016 ~ a2*cin_2015
cin_2017 ~ a2*cin_2016
cin_2018 ~ a2*cin_2017
cin_2019 ~ a2*cin_2018

#ehfs_sp_2012 ~ a1*ehfs_sp_2011
#ehfs_sp_2013 ~ a1*ehfs_sp_2012
#ehfs_sp_2014 ~ a1*ehfs_sp_2013
#ehfs_sp_2015 ~ a1*ehfs_sp_2014
#ehfs_sp_2016 ~ a1*ehfs_sp_2015
#ehfs_sp_2017 ~ a1*ehfs_sp_2016
#ehfs_sp_2018 ~ a1*ehfs_sp_2017
#ehfs_sp_2019 ~ a1*ehfs_sp_2018

# Time specific residual covariance
# ehfs_sp_2011 ~~ cin_2011
ehfs_sp_2012 ~~ cov1*cin_2012
ehfs_sp_2013 ~~ cov1*cin_2013
ehfs_sp_2014 ~~ cov1*cin_2014
ehfs_sp_2015 ~~ cov1*cin_2015
ehfs_sp_2016 ~~ cov1*cin_2016
ehfs_sp_2017 ~~ cov1*cin_2017
ehfs_sp_2018 ~~ cov1*cin_2018
ehfs_sp_2019 ~~ cov1*cin_2019

# Cross-lagged regressions
cin_2012 ~ cl*ehfs_sp_2011
cin_2013 ~ cl*ehfs_sp_2012
cin_2014 ~ cl*ehfs_sp_2013
cin_2015 ~ cl*ehfs_sp_2014
cin_2016 ~ cl*ehfs_sp_2015
cin_2017 ~ cl*ehfs_sp_2016
cin_2018 ~ cl*ehfs_sp_2017
cin_2019 ~ cl*ehfs_sp_2018

ehfs_sp_2012 ~ cin_2011
ehfs_sp_2013 ~ cin_2012
ehfs_sp_2014 ~ cin_2013
ehfs_sp_2015 ~ cin_2014
ehfs_sp_2016 ~ cin_2015
ehfs_sp_2017 ~ cin_2016
ehfs_sp_2018 ~ cin_2017
ehfs_sp_2019 ~ cin_2018


# mean structure - set means to zero except for intercept and slope
cin_2011 ~ 0*1
cin_2012 ~ 0*1
cin_2013 ~ 0*1
cin_2014 ~ 0*1
cin_2015 ~ 0*1
cin_2016 ~ 0*1
cin_2017 ~ 0*1
cin_2018 ~ 0*1
cin_2019 ~ 0*1

i_cin ~ mu_i_cin1*1
s_cin ~ mu_s_cin1*1

ehfs_sp_2011 ~ 0*1
ehfs_sp_2012 ~ 0*1
ehfs_sp_2013 ~ 0*1
ehfs_sp_2014 ~ 0*1
ehfs_sp_2015 ~ 0*1
ehfs_sp_2016 ~ 0*1
ehfs_sp_2017 ~ 0*1
ehfs_sp_2018 ~ 0*1
ehfs_sp_2019 ~ 0*1

i1_exp ~ mu_i_exp1*1
s1_exp ~ mu_s_exp1*1
q_exp ~ mu_q_exp*1

# Unobserved time varying covariates
ehfs_sp_2011 ~~ ehfs_sp_2012
ehfs_sp_2012 ~~ ehfs_sp_2013
ehfs_sp_2013 ~~ ehfs_sp_2014
ehfs_sp_2014 ~~ ehfs_sp_2015
ehfs_sp_2015 ~~ ehfs_sp_2016
ehfs_sp_2016 ~~ ehfs_sp_2017
ehfs_sp_2017 ~~ ehfs_sp_2018
ehfs_sp_2018 ~~ ehfs_sp_2019

cin_2011 ~~ cin_2012
cin_2012 ~~ cin_2013
cin_2013 ~~ cin_2014
cin_2014 ~~ cin_2015
cin_2015 ~~ cin_2016
cin_2016 ~~ cin_2017
cin_2017 ~~ cin_2018
cin_2018 ~~ cin_2019

# define correlations between LVs (needed for bootstrapped correlation estimates)
cor_i_i := cov_icin_iexp / (sqrt(var_i_cin) * sqrt(var_i_exp))
cor_icin_sexp := cov_icin_s1exp / (sqrt(var_i_cin) * sqrt(var_s_exp))
cor_iexp_scin := cov_scin_iexp / (sqrt(var_i_exp) * sqrt(var_s_cin))

cor_i_s_cin := cov_i_s_cin / (sqrt(var_i_cin) * sqrt(var_s_cin))
cor_i_s_exp := cov_i1_s1_exp / (sqrt(var_i_exp) * sqrt(var_s_exp))
cor_sexp_scin := cov_scin_s1exp / (sqrt(var_s_exp) * sqrt(var_s_cin))

